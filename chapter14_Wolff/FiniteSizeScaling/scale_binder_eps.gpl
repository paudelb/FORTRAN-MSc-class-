# Usage:
# Ls = "40 60 80 100 120 140 160 200 500 1000"
# bc = bcc; nu = 1 ; load "scale_binder_eps.gpl";
# Ls: the values of L used in te collapse
# bc: the critical temperature used in the calculation of t=(beta_c-beta)/beta_c
# nu: the exponent used in the calculation of x=L^{1/nu} t


## for the plot in the text see: load "scale_binder_eps.gpl"

bcc   = 0.5*log(1.0+sqrt(2.0)); #the exact critical temperature (use bc=bcc is you wish)
NLs   = words(Ls);  # The number of lattice sizes used in the calculation
LL(i) = word (Ls,i);# Returns the i_th lattice size
cplot(i) = sprintf("<grep '^%s ' allem|sort -k 2.2g|awk -v L=%s -v bc=%f -v nu=%f  '($12/$11)<0.002{print L^(1.0/nu)*(bc-$2)/bc,1.0-$11/3.0,$12}'",LL(i),LL(i),bc,nu);

set macros
set term qt enhanced

set term post eps enha color solid 18 #uncomment if saving to ps
set out  "scale_binder.eps"      #uncomment if saving to eps
set xrange [-40:18]
set yrange [0:0.69]
set multiplot
set origin 0.0,0.0
set size 1.0,1.0

set xlabel "x=L^{1/{/Symbol n}} t"
set ylabel "U(x)"
plot for[i=1:NLs] cplot(i) u 1:2:3 w e t sprintf("L=%s",LL(i)),2.0/3.0 t "2/3"


set origin 0.1,0.2
set size 0.5,0.5
set xrange [-0.35 :0.35  ]
set yrange [0.58:0.63]
set xtics (-0.3,-0.2,-0.1,0,0.1,0.2,0.3)         font "Helvetica,10"
set ytics (0.58,0.59,0.60,0.61,0.62,0.63)    font "Helvetica,10"
set title ""
set xlabel ""
set ylabel ""
set key off
plot for[i=1:NLs] cplot(i) u 1:2:3 w e t sprintf("L=%s",LL(i)),0.61069

unset multiplot

set out
set term qt enhanced
set xrange [*:*]
set yrange [*:*]
set xtics autofreq
set ytics autofreq
set key on
reset
#  ---------------------------------------------------------------------
#  Copyright by Konstantinos N. Anagnostopoulos (2004-2014)
#  Physics Dept., National Technical University,
#  konstant@mail.ntua.gr, www.physics.ntua.gr/~konstant
#  
#  This program is free software: you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation, version 3 of the License.
#  
#  This program is distributed in the hope that it will be useful, but
#  WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
#  General Public License for more details.
#  
#  You should have received a copy of the GNU General Public Liense along
#  with this program.  If not, see <http://www.gnu.org/licenses/>.
#  -----------------------------------------------------------------------
