# Usage:
# Ls = "40 60 80 100 120 140 160 200 500 1000"
# bc = bcc; nu = 1 ; gnu = 1.75; load "scale_gamma_eps.gpl";
# Ls: the values of L used in te collapse
# bc: the critical temperature used in the calculation of t=(beta_c-beta)/beta_c
# nu: the exponent used in the calculation of x=L^{1/nu} t
# gnu: the exponent used in the calculation of F_chi = L^{-gnu} chi(beta,L)

## for the plot in the text see: 

bcc   = 0.5*log(1.0+sqrt(2.0)); #the exact critical temperature (use bc=bcc is you wish)
NLs   = words(Ls);  # The number of lattice sizes used in the calculation
LL(i) = word (Ls,i);# Returns the i_th lattice size
cplot(i) = sprintf("<grep 'm %s ' all|sort -k 3,3g|awk -v L=%s -v bc=%f -v nu=%f -v gnu=%f '($6>0 &&(($7/$6)<0.01)){print L^(1.0/nu)*(bc-$3)/bc,L^(-gnu)*$6,L^(-gnu)*$7}'",LL(i),LL(i),bc,nu,gnu);

set macros
set term qt enhanced
#set title sprintf("{/Symbol b}_c= %f {/Symbol n}= %f {/Symbol g/n}= %f",bc,nu,gnu) #comment if eps
set term post eps enha color solid 18 #uncomment if saving to ps
set out  "scale_gamma.eps"      #uncomment if saving to eps
set xrange [-5:7]
set yrange [0:0.05]
set multiplot
set origin 0.0,0.0
set size 1.0,1.0
set xlabel "x=L^{1/{/Symbol n}} t"
set ylabel "F_{{/Symbol c}}(x) = L^{-{/Symbol g/n}}{/Symbol c}({/Symbol b},L)"
plot for[i=1:NLs] cplot(i) u 1:2:3 w e t sprintf("L=%s",LL(i))

set origin 0.1,0.55
set size 0.4,0.35
set xrange [0.55 :1.3  ]
set yrange [0.046:0.049]
set ytics (0.046,0.047,0.048,0.049) font "Helvetica,10"
set xtics (0.6,0.8,1.0,1.2)         font "Helvetica,10"
set title ""
set xlabel ""
set ylabel ""
set key off
plot for[i=1:NLs] cplot(i) u 1:2:3 w e t sprintf("L=%s",LL(i))

unset multiplot

set out
set term qt enhanced
set xrange [*:*]
set yrange [*:*]
set xtics autofreq
set ytics autofreq
set key on
reset
#  ---------------------------------------------------------------------
#  Copyright by Konstantinos N. Anagnostopoulos (2004-2014)
#  Physics Dept., National Technical University,
#  konstant@mail.ntua.gr, www.physics.ntua.gr/~konstant
#  
#  This program is free software: you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation, version 3 of the License.
#  
#  This program is distributed in the hope that it will be useful, but
#  WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
#  General Public License for more details.
#  
#  You should have received a copy of the GNU General Public Liense along
#  with this program.  If not, see <http://www.gnu.org/licenses/>.
#  -----------------------------------------------------------------------
